<!-- Alfredo Cuevas
     acuevas5 
 -->
 
<!DOCTYPE html>
<html>
<head>
    <title>HW3PartB - Alfredo</title>
    <meta charset="utf-8">
    <style>
        body {
            margin: 0px;
            overflow: hidden;
        }
    </style>
</head>
<body>
    <div id="container"></div>

    <script src="js/three.js"></script>

    <script id="vertexShader" type="x-shader/x-vertex">
        uniform mat4 modelMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;

        attribute vec3 position;
        attribute vec2 uv;
        attribute vec3 normal;

        precision mediump float;

        void main(){
            gl_Position = projectionMatrix * viewMatrix * modelMatrix * vec4(position, 1.0);
        }
    </script>

    <script id="fragmentShader" type="x-shader/x-fragment">
        precision mediump float;

        const int MAX_MARCHING_STEPS = 255;
        const float MIN_DIST = 0.0;
        const float MAX_DIST = 100.0;
        const float EPSILON = 0.0001;

        uniform vec3 eye;
        uniform vec2 u_resolution;
        uniform float fov;
        uniform float time;

        // sphereSDF()
        // Signed distance function for a sphere centered at the origin with radius 1.0.
        float sphereSDF(vec3 samplePoint){
            return length(samplePoint) - 1.0;
        }

        // torusSDF()
        // signed distance function for a torus
        float torusSDF( vec3 samplePoint, vec2 t){
            vec2 q = vec2(length(samplePoint.xz) - t.x, samplePoint.y);
            return length(q) - t.y;
        }

        // Box unsigned - extract
        float boxSDF( vec3 samplePoint, vec3 b){
            return length(max(abs(samplePoint) - b , 0.0));
        }

        // these are the operations that combine shapes
        float opUnion( float d1, float d2 ){
            return min(d1, d2);
        }
        float opSub( float d1, float d2 ){
            return max(-d1, d2);
        }
        float opIntersect( float d1, float d2 ){
            return max(d1, d2);
        }

        float opDisplace(vec3 samplePoint){
            float d1 = sphereSDF(samplePoint);
            float d2 = sin(1.5*samplePoint.x)*sin(1.5*samplePoint.y)*sin(1.5*samplePoint.z);
            return d1+d2;
        }
        float opTwistTor( vec3 samplePoint ){
            float c = cos(2.0 * samplePoint.y);
            float s = sin(2.0 * samplePoint.y);
            mat2 m = mat2(c, -s, s, c);
            vec3 q = vec3(m * samplePoint.xz, samplePoint.y);
            return torusSDF(q, vec2(0.7, .5));
        }

        // creates a smooth blend between two objects that I created
        float smin( float a, float b, float k ){
            float h = clamp( 0.5+0.5*(b-a)/k, 0.0, 1.0 );
            return mix( b, a, h ) - k*h*(1.0-h);
        }
        float opBlend( vec3 samplePoint ){
            float d1 = opDisplace(samplePoint);
            float d2 = sphereSDF(samplePoint);
            return smin( d1, d2, 0.1 );
        }

        // sceneSDF()
        // Signed distance function describing the scene.
        // Absolute value of the return value indicates the distance to the surface.
        // Sign indicates whether the point is inside or outside the surface, negative indicating inside.
        float sceneSDF(vec3 samplePoint){
            float sphere = sphereSDF(samplePoint);
            float box = boxSDF(samplePoint, vec3(0.7, 0.7, 0.7));
            float torus = torusSDF(samplePoint, vec2(1.0, .3));
            float shape1 = opSub(torus,opUnion(box, sphere));
            float shape2 = box;
            return mix(shape2, shape1, sin(time*0.5));

            //return mix(shape2 , opUnion(sphereSDF(samplePoint), boxSDF(samplePoint, vec3(0.7, 0.7, 0.7))), sin(time));

            // Experiments---------Don't try--------
            //return mix(opIntersect(sphere, box), opSub(opTwistTor( samplePoint ),box), sin(time));
            //return mix(opDisplace(samplePoint), opUnion(torus, box) , sin(time* 0.1));
            //return opIntersect(opIntersect(sphere, torus), box);
            //return mix( opUnion(box, sphere), opSub(sphere, torus), cos(time));
            //return torusSDF(samplePoint, vec2(.5, 0.3));
        }

        // shortestDistanceToSurface()
        // Return the shortest distance from the eyepoint to the scene surface along the marching direction.
        // If no part of the surface is found between start and end, return end.
        //
        // eye: the eye point, acting as the origin of the ray
        // marchingDirection: the normalized direction to march in 
        // start: the starting distance away from the eye
        // end: the max distance away from the eye to march before giving up
        float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end){
            float depth = start;
            for(int i = 0; i < MAX_MARCHING_STEPS; i++){
                float dist = sceneSDF(eye + (depth * marchingDirection));
                if(dist < EPSILON){
                    return depth;
                }
                depth += dist;
                if(depth >= end){
                    return end;
                }
            }
            return end;
        }

        // rayDirection()
        // Return the normalized direction to march in from the eye point for a single pixel
        // fieldOfView: vertical field of view in degrees
        // size: resolution of the output image
        // fragCoord: the x, y coordinate of the pixel in the output image
        vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord){
            vec2 xy = fragCoord - (size / 2.0); // places the shape origin in the middle of the screen
            float z = size.y / tan(radians(fieldOfView) / 2.0);
            return normalize(vec3(xy, -z));
        }

        mat3 rayMarchViewMatrix(vec3 cam, vec3 center, vec3 up){
            vec3 f = normalize(center - cam);
            vec3 s = normalize(cross(f, up));
            vec3 u = cross(s, f);
            return mat3(s, u, -f);
        }

        // estimateNormal()
        vec3 estimateNormal(vec3 p){
            return normalize(vec3(
                sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
                sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
                sceneSDF(vec3(p.x, p.y, p.z + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
            ));
        }

        // phongContribForLight()
        // Lighting contribution of a single point light source via Phong Illumination.
        // the vec3 returned is the RGB color of the light's contribution.
        // k_a: Ambient color
        // k_d: Diffuse color
        // k_s: Specular color
        // alpha: Shininess coefficient
        // p: position of point being lit
        // eye: the position of the camera
        // lightPos: the position of the light
        // lightIntensity: color/intensity of the light
        vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye, vec3 lightPos, vec3 lightIntensity){
            vec3 N = estimateNormal(p);
            vec3 L = normalize(lightPos - p);
            vec3 V = normalize(eye - p);
            vec3 R = normalize(reflect(-L, N));

            float dotLN = dot(L, N);
            float dotRV = dot(R, V);

            if( dotLN < 0.0){
                // light not visible from this point on the surface
                return vec3(0.0, 0.0, 0.0);
            }

            if( dotRV < 0.0){
                // light reflection in opposite direction as viewer, apply only diffuse component
                return lightIntensity * (k_d * dotLN);
            }
            return lightIntensity * (k_d * dotLN + k_s * pow(dotRV, alpha));
        }

        // phongIllumination()
        // Lighting via Phong Illumination
        // the vec3 returned is the RGB color of the point after lighting is applied.
        // k_a: Ambient color
        // k_d: Diffuse color
        // k_s: Specular color
        // alpha: Shininess coefficient
        // p: position of point being lit
        // eye: the position of the camera
        vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye){
            const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
            vec3 color = ambientLight * k_a;

            vec3 light1Pos = vec3(4.0 * sin(time), 2.0, 4.0 * cos(time));
            vec3 light1Intensity = vec3(0.4, 0.4, 0.4);

            color += phongContribForLight(k_d, k_s, alpha, p, eye, light1Pos, light1Intensity);

            vec3 light2Pos = vec3(-2.0 * sin(time), -2.0 * cos(time), 2.0);
            vec3 light2Intensity = vec3(0.4, 0.4, 0.4);

            color += phongContribForLight(k_d, k_s, alpha, p, eye, light2Pos, light2Intensity);

            return color;
        }


        void main(){

            vec3 dir = rayDirection(fov, u_resolution, gl_FragCoord.xy);
            //vec3 eye = vec3(-4.0, 5.0, 7.0);

            mat3 viewToWorld = rayMarchViewMatrix( eye, vec3(0.0, 0.0, 0.0), vec3(0.0, 1.0, 0.0));
            vec3 worldDir = viewToWorld * dir;

            float dist = shortestDistanceToSurface(eye, worldDir, MIN_DIST, MAX_DIST);

            if(dist > ( MAX_DIST - EPSILON )){
                // didn't hit anything
                gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
            }else{

                // The closest point on the surface to the eyepoint along the view ray
                vec3 p = eye + (dist * worldDir);

                vec3 K_a = vec3(0.2, 0.2, 0.2);
                vec3 K_d = vec3(0.2, 0.7, 0.2);
                vec3 K_s = vec3(1.0, 1.0, 1.0);
                float shininess = 10.0;

                vec3 color = phongIllumination( K_a, K_d, K_s, shininess, p, eye );

                gl_FragColor = vec4(color, 1.0);
            }
        }
    </script>

    <script>
        var container;
        var camera, scene, rederer, mesh;
        var fov = 45.0;

        var vs = document.getElementById('vertexShader').textContent;
        var fs = document.getElementById('fragmentShader').textContent;

        init();
        update();

        function init(){

            container = document.getElementById('container');

            var width = window.innerWidth;
            var height = window.innerHeight;

            camera = new THREE.OrthographicCamera(width / -2, width / 2, height / 2, height / -2, 0.1, 1000);
            camera.position.z = 10;

            scene = new THREE.Scene();

            //geometry
            var geometry = new THREE.PlaneGeometry( width, height, 1, 1);

            //material
            var uniforms = {
                u_resolution: { type: "v2", value: new THREE.Vector2(width, height) },
                fov: {type: "f", value: fov },
                time: {type: "f", value: 0.0 },
                eye: {type: "v3", value: new THREE.Vector3(8.0, 5.0, 7.0) },
            };

            var material = new THREE.RawShaderMaterial({
                uniforms: uniforms,
                vertexShader: vs,
                fragmentShader: fs,
            });

            mesh = new THREE.Mesh( geometry, material );
            scene.add(mesh);

            renderer= new THREE.WebGLRenderer();
            renderer.setSize(window.innerWidth, window.innerHeight);
            container.appendChild( renderer.domElement );

            window.addEventListener( 'resize', onWindowResize, true);
        }

        function update(){
            requestAnimationFrame( update );
            mesh.material.uniforms.time.value += 0.01;
            //mesh.material.uniforms.eye.value.x += 0.01;

            
            render();
        }

        function render(){
            renderer.render(scene, camera);
        }

        function onWindowResize( event ){
            camera.aspect = window.innerWidth / window.innerHeight;
            camera.updateProjectionMatrix();

            renderer.setSize( window.innerWidth, window.innerHeight );
            mesh.material.uniforms.u_resolution.value = new THREE.Vector2(window.innerWidth, window.innerHeight);
        }
    </script>

</body>
</html>